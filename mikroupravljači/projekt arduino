#include <Keypad.h>
#include <LiquidCrystal_I2C.h>

LiquidCrystal_I2C lcd(0x27,16,2);

const byte REDOVI = 4, STUPCI = 4;
char hexaKeys[REDOVI][STUPCI] = {
  {'1', '2', '3', 'A'}, 
  {'4', '5', '6', 'B'},
  {'7', '8', '9', 'C'},
  {'*', '0', '#', 'D'}
};
byte redPinovi[REDOVI] = {2, 3, 4, 5}, stupacPinovi[STUPCI] = {6, 7, 8, 9};
Keypad tipkovnica = Keypad(makeKeymap(hexaKeys), redPinovi, stupacPinovi, REDOVI, STUPCI);

const int trigPin = 11, echoPin = 12, buzzer = 10, ledPin = 13;
String lozinka = "", tocnaLozinka = "1234A";
bool alarm = false;

void postaviPinove() {
  pinMode(trigPin, OUTPUT);
  pinMode(echoPin, INPUT);
  pinMode(buzzer, OUTPUT);
  pinMode(ledPin, OUTPUT);
  digitalWrite(ledPin, LOW);
  noTone(buzzer);
}

void prikaziPoruku(String poruka) {
  lcd.clear();
  lcd.print(poruka);
  Serial.println(poruka);
}

void provjeriLozinku() {
  if (lozinka == tocnaLozinka) {
    alarm = !alarm;
    if (alarm) {
    prikaziPoruku("Alarm aktiviran");
  } else {
    prikaziPoruku("Alarm deaktiviran");
  }
    digitalWrite(ledPin, !alarm);
    noTone(buzzer);
  } else {
    prikaziPoruku("Pogresna lozinka");
  }
  lozinka = "";
}

long mjeriUdaljenost() {
  digitalWrite(trigPin, LOW);
  delayMicroseconds(2);
  digitalWrite(trigPin, HIGH);
  delayMicroseconds(10);
  digitalWrite(trigPin, LOW);
  return pulseIn(echoPin, HIGH) * 0.034 / 2;
}

void setup() {
  Serial.begin(9600);
  lcd.init();
  lcd.backlight();
  postaviPinove();
}

void loop() {
  char znak = tipkovnica.getKey();
  if (znak) {
    if (znak == '*') {
      lozinka = "";
      prikaziPoruku("Unos resetiran");
    } else {
      lozinka += znak;
      prikaziPoruku("Uneseno: " + String(znak));
      if (lozinka.length() >= tocnaLozinka.length()) provjeriLozinku();
    }
  }
  
  if (alarm && mjeriUdaljenost() < 5) {
    tone(buzzer, 1000);
    digitalWrite(ledPin, LOW);
  } else {
    noTone(buzzer);
  }
}
